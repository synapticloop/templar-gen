plugins {
	id 'java'
	id 'eclipse'
	id 'maven'
	id 'maven-publish'

	id 'net.saliman.cobertura' version '2.2.6'
	id 'co.riiid.gradle' version '0.3.1'
	id 'com.jfrog.bintray' version '1.4'

	id 'synapticloop.documentr' version '1.2.3'
	id 'synapticloop.copyrightr' version '1.0.1'
	id "synapticloop.projectFilestatistics" version "1.0.1"

	id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group = 'synapticloop'
archivesBaseName = 'templar-gen'
description = """generate output with the Templar templating engine"""
version = '1.2.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7


cobertura {
	coverageFormats = [ 'html', 'xml']
}

test {
	include '**/*Test.class' 
	maxParallelForks = 1
	forkEvery = 50 
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	runtime 'synapticloop:templar:1.3.0'

	compile 'synapticloop:templar:1.3.0'
}



jar {
	manifest {
		attributes("Main-Class": "synapticloop.templar.gen.Main")
	}
}

shadowJar {
	classifier = ''
}

jar.finalizedBy(project.tasks.shadowJar)

def javaApiUrl = 'http://docs.oracle.com/javase/1.6.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'

tasks.withType(Javadoc) {
	options.links(javaApiUrl, groovyApiUrl)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

test.finalizedBy(project.tasks.cobertura)

publishing {
	publications {
		Synapticloop(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId 'synapticloop'
			artifactId 'templar-gen'
		}
	}
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_PASSWORD')

	publications = [ 'Synapticloop' ]

	publish = true

	pkg {
		repo = 'maven'
		name = "templar-gen"
	}
}

copyrightr {
	dryRun = false
	excludes = [
		"src/test/java/**/*.java"
	]
}

github {
	owner = group
	repo = archivesBaseName
	if(System.getenv('GITHUB_TOKEN')) {
		token = System.getenv('GITHUB_TOKEN')
	}
	tagName = version
	name = version
	assets = [
		'build/libs/' + archivesBaseName + '-' + version + '.jar'
	]
}